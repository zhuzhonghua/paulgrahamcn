---
layout: post
title : "第2章--欢迎来到 Lisp"
category : acl
tags : [acl]
---
{% include JB/setup %}


就像有人说的，Lisp的语法可以在一个小时内全部讲完。
本节也是，先对Lisp有个大概的了解。之后如果挖，请深挖。    


这就和学习大多数其它东西类似，先写出一个helloworld级别的东西，然后再深入学习Manual。  


##2.1 Form  


学习Lisp应该一边学一边用，因为Lisp是一门交互性的语言。
推荐使用CLISP。
如果一定要使用IDE的话，推荐使用Emacs＋SLIME。  


进入CLISP之后有个交互前端，这叫toplevel。
在这里输入一些表达式，就会同时把结果输出。   


最简单的表达式，当然是数字了。    


	> 1
	1
	>


接下来作一些计算。  


	> (+ 2 3)
	5
	>


在这里+是操作符，2和3是参数。  


这就是传说中的前缀表达式，操作符在第一位，参数在后面。这样如果写类似2+3+4时，需要这样写  


    (+ 2 3 4)	


也就是说，+可以有任意多的参数。你可以自己试试没有任何参数的+表达式。  


如果需要嵌套\+\-\*\/，只需要空格分开，括号匹配就可以。  


    (+ (/ 4 2)
	   (* 2 4))  


##2.2 求值  


1. 从左到右，把参数逐个求值。  
2. 把求值结果作为参数传递给操作符。  


如果你看过SICP，就知道这并不是唯一的方式。不过，现在这种方式是CLISP系统所采用的方式。  


如果参数本身也是函数调用，或者使用了另外的操作符，那么递归应用这两条规则。  


CL中不是所有的操作符都是函数，但大多数都是函数，并且遵循这两条规则。其中一个不遵循这种方式的操作符就是quote。quote操作符是一个特殊的操作符，有单独的规则。这条规则就是：无为。quote操作符接受一个参数，然后只是把参数作为结果返回。  


	> (quote (+ 2 5))
	(+ 2 5)
	>


有一种简写形式，可以达到一样的目的。  


	> '(+ 2 5)
	(+ 2 5)
	> 


这样可以避免表达式求值。为什么要避免表达式求值呢？   


##2.3 数据


Lisp中有很多数据类型，如数字，字符串，这些可以自求值。
还有两个特殊的：符号(Symbol)和列表(list)。   


符号会全部转换为大写   


	> 'Artichoke
	ARTICHOKE
	> 


符号不会自求值，所以如果要想用符号，就必须使用quote。  


列表是一对括号里包含了0个或多个元素。   


列表元素可以是任意类型，甚至还是列表。
列表也必须使用quote，要不然就会被求值。   


	> '(my 3 "Sons")
	(MY 3 "Sons")
	> '(the list (a b c) has 3 elements)
	(THE LIST (A B C) HAS 3 ELEMENTS)
	>


可以使用list来构造列表，因为list是函数，所以参数会被求值。   


	>(list 'my (+ 2 1) "Sons")
	(MY 3 "Sons")
	>


接下来就是见证奇迹的时刻：lisp程序是由列表构成，列表还是一种数据类型。
你看到这句话的点了么？不急，慢慢来。  


	> (list '( + 2 1) ( + 2 1))
	((+ 2 1) 3)
	>


所以看到上面，你懂了，加上quote，它就是数据类型，不会求值。去掉quote，它就是程序，会求值。


Lisp里这样表示空。  


	> ()
	NIL
	> nil
	NIL
	>


##列表操作  


前面介绍了list函数可以构造列表，还有另外一个函数也可以，cons。


	> (cons 'a '(b c d))
	(A B C D)
	>


cons接受两个参数，当第二个参数类型是列表时，cons函数返回一个新的列表，即使第两个参数是空表。


如果第两个参数不是列表，你可以自己试一下，后面讲。  


	> (cons 'a (cons 'b nil))
	(A B)
	> (list 'a 'b)
	(A B)
	>


所以如果第二个参数是空表，这样写就和使用list一样了。  


在列表中获取元素的两个主要方法是car(Contents of the Address part of Register number)和cdr(Contents of the Decrement part of Register number)。  


	[3]> (car '(a b c))
	
	A
	[4]> (cdr '(a b c))
	
	(B C)
	[5]>


car 就是取列表的第一个元素，cdr就是剩下的东东。  


这样使用两个操作的交叉重复使用就可以获取列表里的任意个元素。  


	[5]> (car (cdr (cdr '(a b c d))))
	
	C
	[6]>


还可以使用快捷方式：



	[6]> (third '(a b c d))
	
	C


##2.5 真假  


符号t表示真，相对的nil表示假。如listp判断参数是否为列表：  


	Break 1 [8]> (listp '(a b c))
	
	T
	Break 1 [8]> 	


	Break 1 [8]> (listp 27)

	NIL
	Break 1 [8]>


这样nil有两种含义，空表：  


	Break 1 [8]> (null nil)
	
	T
	Break 1 [8]>


假：  


	Break 1 [8]> (not nil)
	
	T
	Break 1 [8]>


条件表达式的格式是if then else，注意：没有else if。  


	Break 1 [8]> (if (listp '(a b c))
			 (+ 1 2)
			 (+ 5 6))
	
	3
	Break 1 [8]> (if (listp 2)
			 (+ 1 2)
			 (+ 5 6))
	
	11
	Break 1 [8]>


所以要想写else if的话，要在else部分继续嵌套。
这样的设计哲学有点类似car和cdr，使用基本的原语，来构造复杂的语句。  


获取列表元素的基本原语是car和cdr，条件表达式的基本原语是if else。当然下面的cond是另外一种形式的基本原语。  


当然如果else部分不写任何东西，就是nil。  


	Break 1 [8]> (if (listp 2)
			 (+ 1 2))
	
	NIL
	Break 1 [8]>


t表示true，可以放在if 之后的测试里，其它非nil的表达式也都可以作为测试表达式。  


	Break 1 [8]> (if 't
			 1)
	
	1
	Break 1 [8]> (if 2
			 1)
	
	1
	Break 1 [8]>


and 和 or类似条件表达式，但是可以接受任意多的参数，并且根据需要来对参数求值。
如果所有参数都非nil，那么返回最后一个参数的求值结果。  


	Break 1 [8]> (and t (+ 1 2))
	
	3
	Break 1 [8]>


如果有一个参数是nil，那么之后参数都不会被求值，即使后面的参数是非法表达式，然后返回nil。  


	Break 1 [8]> (and nil (+ 1 2))
	
	NIL
	Break 1 [8]> (and (+ 1 2) nil (+ a c))
	
	NIL
	Break 1 [8]>


而or呢？只要找到一个参数非nil，那么就停下来，返回那个值。要么所有参数都是nil，返回nil。  


	Break 1 [8]> (or (+ 1 2) nil)
	
	3
	Break 1 [8]> (or nil ())
	
	NIL
	Break 1 [8]>


and 和 or 是宏，比较特殊，可以绕过一般的求值规则，后面讲。   


##2.6 函数  


可以使用defun定义函数，参数有：函数名，参数，函数体。函数体可以有多个表达式。  


	Break 1 [8]> (defun our-third(x)
			(car (cdr (cdr x))))
	
	OUR-THIRD
	Break 1 [8]>


可以这样调用：  


	Break 1 [8]> (our-third '(a b c d))
	
	C
	Break 1 [8]>


判断1 加上 4是不是大于3：  


	Break 1 [8]> (> (+ 1 4) 3)
	
	T
	Break 1 [8]>


可以写一个函数，来代替：   


	Break 1 [8]> (defun sum-greater(x y z)
			(> (+ x y) z))
	
	SUM-GREATER
	Break 1 [8]> (sum-greater 1 4 3)
	
	T
	Break 1 [8]>


所以可以认为函数是一系列表达式的封装，简写。  


##2.7 递归  


sum-greater函数调用了>和+，如果sum-greater调用了sum-greater了，会怎么样？或者说，一个函数调用了自己。这样的话叫递归。  


CL有一个函数member，测试某元素是不是某列表的其中一项。可以这样写：  


	Break 1 [8]> (defun our-member(obj lst)
			(if (null lst)
			    nil
			    (if (eql obj (car lst))
			        lst
			        (our-member obj (cdr lst)))))
	
	OUR-MEMBER
	Break 1 [8]>


eql可以检测两个参数是否相等。其实相等有两种情况，下面再讲。  


	Break 1 [8]> (our-member 'b '(a b c))
	
	(B C)
	Break 1 [8]> (our-member 'z '(a b c))
	
	NIL
	Break 1 [8]>


对于c/c++程序员来说，递归一点也不陌生。著名的汉诺塔问题，用递归非常简单的就搞定了。  


lisp对于递归有一个优化，尾递归。这和其它程序，尤其是c/c++程序有明显的区别。尾递归可以在网上搜到很多资料。
如果从函数调用上来讲，是尾递归；
但是如果从数学角度讲，可以称为迭代。	


##2.8 阅读Lisp  


很多人头疼Lisp的一个原因就是他的括号，因为我们习惯看右括号之后找左括号，或者看完左括号之后，找右括号。
就像我们在C程序里写括号一样，甚至大括号。
所以即使我们写的代码格式很乱，还是依稀能分别出一些模块功能。  


但是在Lisp里，千万别这么做。因为很疼。
所以完全有理由假设你要看的Lisp代码是很好的格式化过的，如果没有，那么就不要看。


所以你自己写代码时，如果非得看看左右括号，那么应该找一个支持此特性的编辑器。
看看Python，做的更绝，直接把良好的格式作为一种语法。  


所以还是养成良好习惯吧，亲。  


##2.9 输入与输出  


CL中有一个输出函数format。
它有两个或多个参数，第一个参数就是目的地，第二个参数是一个模板，后面的参数根据要求填入模板中。  


	CL-USER> (format t "~A plus ~A equals ~A~%" 2 3 (+ 2 3))
	2 plus 3 equals 5
	NIL
	CL-USER>

第一个参数t是指默认的地方，也就是toplevel。
第二个参数的是模板，~A是一个位置，由后面的参数填充，~%是新行，回车。


C程序员，应该对这个不陌生，不同的地方在于这里不需要指定参数类型。  


标准的输入函数是read。如果没有参数，那就是从默认的地方读，也就是toplevel。  


	(defun askem(string)
	  (format t "~A" string)
	  (read))
	  
	CL-USER> (askem "How old are you?")
	How old are you?27
	27
	CL-USER>


read 会一直等待，直到你回车了。


##2.10 变量  


在CL中使用的最多的是let操作符，这样可以定义局部变量。


	CL-USER> (let ((x 1) (y 1))
		    (+ x y))
	2
	CL-USER>


let有两部分，定义和体。定义部分可以有多个表达式，每个表达式都是(var value)格式。
这些定义的var只是在let表达式内有效。
体部分可以有多个表达式，最后一个表达式的值作为整体的值返回。  


	(defun ask-number()
	  (format t "enter a number:")
	  (let ((p (read)))
	     (if (numberp p)
		p
		(ask-number))))


定义变量p，把输入传给它。使用numberp检测输入是否是数字，如果是返回。如果不是递归调用ask-number。  

			
	CL-USER> (ask-number)
	enter a number:r
	enter a number:()
	enter a number:4
	4
	CL-USER>


还可以使用defparameter定义全局变量：  


	CL-USER> (defparameter *glob* 99)
	*GLOB*
	CL-USER>


使用defconstant定义全局常量：  


	CL-USER> (defconstant limit (+ *glob* 99))
	LIMIT
	CL-USER>


全局常量与全局变量的区别这里就不讲了。  


如果希望检测一下某Symbol是否已经定义了，可以使用boundp  


	CL-USER> (boundp '*glob*)
	T
	CL-USER>


##2.11 赋值  


CL中最常用的赋值操作符是setf  


	CL-USER> (setf *glob* 88)
	88
	CL-USER> (let ((n 2))
		   (setf n 3)
		   n)
	3
	CL-USER>


如果setf的第一个参数是非局部变量的符号，那么该符号就会定义为全局变量。  


	CL-USER> (setf x (list 'a 'b 'c))
	(A B C)
	CL-USER>


setf还可以给表达式赋值，如： 


	CL-USER> (setf (car x) 'N)
	N
	CL-USER> x
	(N B C)
	CL-USER>


setf还可以有多个参数，如：  


	(setf a 'b
	      c 'd
	      e 'f)


这样就定义了a c e三个变量。  


##2.12 函数编程 


一般函数编程的意思是说，写代码时，尽量操作返回值，而不要去修改东西。


	Break 1 [6]> (setf lst '(c a r a t))
	
	(C A R A T)
	Break 1 [6]> (remove 'a lst) 
	
	(C R T)
	Break 1 [6]> lst
	
	(C A R A T)
	Break 1 [6]>


remove虽然把lst中的a删除了，但是它并没有修改lst，而是返回了一个新的列表，在新的列表里删除了a。lst本身并没有变化。
如果真的想要从lst中删除a，那么可以这样写：  


    (setf lst (remove 'a lst))


函数编程的本质是，避免类似setf之类的操作。


WOW，这怎么可能？我可以只需要操作返回值就可以写程序了牛B的程序了？  


##2.13 迭代  


迭代，递归，尾递归，这么多概念。


不过让我们先来写一个函数：


	(defun show-squares (start end)
	  (do ((i start (+ i 1)))
	      ((> i end) 'done)
	    (format t "~A ~A~%" i (* i i))))
	
	SHOW-SQUARES
	Break 1 [6]> (show-squares 2 5)
	2 4
	3 9
	4 16
	5 25
	DONE
	Break 1 [6]>


do是CL中最基础的迭代操作。第一个参数是变量声明列表，每个元素的格式如下：  


    (var initial update)  


var是符号，initial和update是表达式。var 首先被设置为initial，之后每次迭代被设置为update。  


像show-squares里的do，i最初被设置为start，之后每次迭代都做＋1操作。  


do的第二个参数可以是1个或者多个表达式。
第一个表达式作为测试，判断迭代是否需要停止。
如果成功，那么之后的表达式逐个求值，最后一个作为返回值。  


在上面的例子中，判断表达式是(> i end)，返回值是'done。  


所有后面的表达式组成了循环体，在每次迭代时，逐个求值。  


我们比较一下show-squares的递归版本：  


	(defun show-squares (i end)
	  (if (> i end)
	      'done
	      (progn
		(format t "~A ~A~%" i (* i i))
		(show-squares (+ i 1) end))))


progn的意思是逐个执行后面的表达式，返回最后一个值。  


CL有一个简单的操作符来遍历一个列表。  


	(defun our-length (lst)
	  (let ((len 0))
	    (dolist (obj lst)
	      (setf len (+ len 1)))
	    len))


此函数的递归版本如下：  

	
	
	(defun our-length (lst)
	  (if (null lst)
	      0
	      (+ (our-length (cdr lst)) 1)))

	
attention: 此版本不是尾递归版本。我们后面讲尾递归。


##2.14 函数也是对象


我们可以用函数function来看一下：


	Break 2 [4]> (function -)
	
	#<SYSTEM-FUNCTION ->
	Break 2 [4]>


你或许会想，如果把function本身传给function会发生什么？
恩。这个你可以试一下，但是Lisp里有一种东西叫特殊操作符，function就是。
所以你把function传给function，是错误的。因为function的参数必须是函数，而function不是函数。
那它是什么呢？我说了，是特殊操作符。


可以使用#'代替function，就像使用'代替quote一样。


	Break 3 [5]> #' -
	
	#<SYSTEM-FUNCTION ->
	Break 3 [5]>


既然函数也是对象，那么我们可以把函数作为参数来使用，如apply。  


	Break 4 [6]> (apply #'+ '(1 2 3) )
	
	6


apply接受一个函数还有N个后续参数，然后把所有的参数都传给它的函数参数。
但是只有一个要求，最后一个参数必须是列表。


	Break 4 [6]> (apply #'+ 1 2 3 '(1 2 3) )
	
	12
	Break 4 [6]>


或者使用funcall，它不要求最后一个参数是列表，并且相反，它要求后面的参数必须与它的函数对象的参数匹配。


	Break 5 [7]> (funcall #'+ 1 2 3 (+ 2 3))
	
	11
	Break 5 [7]>


defun定义一个有名字的函数，还可以使用lambda定义没有名字的函数。
我们会有需求要使用lambda来定义没有名字的函数的，相信我。


	(lambda (x y)
	  (+ x y))


lambda代替的就是defun+函数名。  


lambda定义的也是一个函数，只是没有名字而已。但是依然可以作为函数来调用，如


	Break 5 [7]> ((lambda (x) (+ x x)) 1)
	
	2
	Break 5 [7]>


当然我们也可以把lambda表达式定义的函数作为参数传给apply或者funcall。  


	Break 7 [9]> (apply #'(lambda (x) (+ x x)) '(1))
	
	2
	Break 7 [9]> (funcall #'(lambda (x) (+ x x)) 1)
	
	2
	Break 7 [9]>


##2.15 类型


CL中，任意变量可以指向任意类型的值。声明变量时不需要指定类型。
但是我们可以知道某值是什么类型的。


CL中的一个对象可以是多种类型的，就像继承一样。
如数字27可以是fixnum, integer , rational, real, number, atom, t。
t是所有类型的父类。


	Break 7 [9]> (typep 27 'integer)
	
	T
	Break 7 [9]> 

	
##2.16 明天


Lisp的语法很简单，简单到只有列表。加上quote就是数据，去掉quote就是程序。
后面会慢慢理解代码与数据的统一。
这也是我们代码的明天，是过去现在和将来的明天。  
