---
layout: post
title : "第5章--控制流"
category : acl
tags : [acl]
---
{% include JB/setup %}


前面一部分已经涉及到控制流，if else do等等。
这里再讲点别的。  


##5.1 程序块  


创建一块代码有三个操作符，progn, block, tagbody。  


progn就是顺序执行几个表达式，把最后一个表达式的值返回。和c/c++的逗号表达式很类似。  


	CL-USER> (progn
		   (format t "a")
		   (format t "b")
		   (+ 11 12))
	ab23
	CL-USER> 


block带一个名字，标明这个block，还可以在块里写return-from xxx，这样可以中途停止block的执行。  


	CL-USER> (block head
		   (format t "Here We Go.")
		   (return-from head 'idea)
		   (format t "W'll Never see this."))
	Here We Go.IDEA
	CL-USER> 


return-from的第二个参数就作为该block的值。  


还有一个return也可以中途返回值，不过只能对名为nil的block使用，也就是  


	CL-USER> (block nil
		   (return 27))
	27
	CL-USER> 


为什么会有名为nil的block呢？  


	CL-USER> (dolist (x '(a b c d e))
		   (format t "~A " x)
		   (if (eql x 'c)
		       (return 'Done)))
	A B C DONE
	CL-USER> 


你可以认为上面的一段代码是放在一个默认的名字为nil的block内的。并且事实上就是这样。


而我们定义一个函数时，事实上我们就定义了一个名字为函数名的block。  


	(defun foo ()
	  (return-from foo 27))

	
而在block或者函数，甚至前面的默认名字为nil的block，在这些代码之外使用return是没有用的。  


我们可以使用return-from定义一个read-integer的新版本。  


	(defun read-integer-3 (str)
	  (let (accum 0)
	    (dotimes (pos (length str))
	      (let ((i (digit-char-p (char str pos))))
		(if i
		    (setf accum (+ (* accum 10) i))
		    (return-from read-integer-3 accum))))
	    accum))

	
这样当我们遇到一个非数字的字符时，就可以直接返回。  


在tagbody内，可以使用go，当然要先指定标签。  


	CL-USER> (tagbody
		  (setf x 0)
		  top
		  (setf x (+ x 1))
		  (format t "~A " x)
		  (if (< x 10) (go top)))
	1 2 3 4 5 6 7 8 9 10 NIL
	CL-USER> 


上面是一段非常丑陋的代码，仅仅作为示例。  


其实很多的控制流都是基于tagbody的go，但是一般我们写代码时都不这么写。
如果需要顺序执行，那就使用progn。如果需要中途返回，那么就用block。除非你有确切的理由，最好不要用tagbody。  


##5.2 上下文  


下面我们再来看看let表达式。  


	CL-USER> (let ((x 7)
		       (y 2))
		   (format t "Number ")
		   (+ x y))
	Number 9
	CL-USER> 


虽然我们在let表达式内定义了变量，但是如果你像下面这么写，那么你就掉坑里了。  


	(let ((x 7)
	      (y (+ x 2)))
	  (+ x y))
	

为什么不能这么写呢？  


你可以认为let表达式是我们调用了一个匿名函数，就是lambda。所以上面的代码翻译成  


	((lambda (x y) (+ x y))
	 7
	 2)


这样理解是不是舒服点？如果你一定要写一个那样的let，那么你应该使用的是let\*，而不是let。  


	CL-USER> (let\* ((x 7)
			(y (+ x 2)))
		   (+ x y))
	16
	CL-USER> 


let\*可以理解为嵌套的let，所以上面的代码可以翻译为  


	(let ((x 7))
	  (let ((y (+ x 2)))
	    (+ x y)))


在let和let\*里，如果某变量只声明，为指定值，那么默认为nil。  


	CL-USER> (let (x y)
		   (list x y))
	(NIL NIL)
	CL-USER> 


destructuring-bind是let的一般化。
它的变量初始化是可以指定一个模式，如果变量和值的模式不匹配，那么回出错。  


	CL-USER> (destructuring-bind (w (x y) . z) '(a (b c) d e)
				    (list w x y z))
	(A B C (D E))
	CL-USER> 


这里有个小技巧，防止出错。那就是那个点。  


##5.3 条件  


最简单的就是if。很多条件判断都可以在if之上构建，比如when。  


	(when (oddp that)
	  (format t "Hmm. That's Odd")
	  (+ oddp 1))


这段代码可以翻译为if。  


	(if (oddp that)
	    (progn
	      (format t "Hmm. That's Odd")
	      (+ oddp 1)))


when的反向使用操作符是unless，除了条件判断相反，其它一样。  


最基本的条件判断是cond，可以接受多个判断条件。每个判断之后执行的语句可以认为是匿名的progn。
如果你要写一个嵌套的if if，那么就应考虑使用cond。  


如下面这段代码  


	(defun our-member(obj lst)
	   (if (null lst)
		   nil
		   (if (eql obj (car lst))
			   lst
			   (our-member obj (cdr lst)))))


就可以写成  


	(defun our-member(obj lst)
	   (cond ((null lst) nil)
		 ((eql obj (car lst)) lst)
		 ((our-member obj (cdr lst)))))


事实上很多Lisp实现都把cond实现为if，不过就像前面说的如果需要嵌套的if，那么就应该考虑使用cond。
这样让我们的代码看起来更简洁。  


cond的每条语句都是有条件+默认的progn组成。如果默认的progn没有提供，那么就返回条件。


	CL-USER> (cond (99))
	99
	CL-USER> 


在使用cond时最好能够在最后加一个t这样的条件，这是一个很好的习惯。
就和c/c++里的switch-case的default一样。  


如果所有条件都判断失败，那么就只能返回nil了。  


如果想用一个变量比较多个条件，可以使用case。  
比如根据指定月份返回当月天数：  


	(defun month-length (mon)
	  (case mon
	    ((jan mar may jul aug oct dec) 31)
	    ((apr jun sept nov) 30)
	    (feb (if (leap-year) 29 28))
	    (otherwise "unknown month")))


case首先有一个要比较的变量，之后是一系列语句。
每个语句以一个key或者一个key列表开始，之后是一系列表达式。当然这一系列表达式或者一系列语句可以是零条。


每个语句开始的key或者一个key列表，会被解析为常量，而不会被执行，所以不用加quote。  


比较使用的是eql。如果有一个比较成功，那么就执行后面的一系列表达式，然后把最后一个表达式的值返回。  


最后一个表达式可以是t或者otherwise。如果没有匹配的key或者key后面没有表达式，那么只能返回nil了。  


	CL-USER> (case 99 (99))
	NIL
	CL-USER> 


还有一个类似的typecase，只不过它比较的是类型，使用的比较操作符是typep，而不再是eql。  


##5.4 迭代  


最基本的迭代操作当然是do了，还可以结合return等其它操作。  


do的初始化列表的格式是一系列的    


	(variable initial update)


update可以被省略，你知道不会发生什么。
initial也可以被省略，这样variable的初始值就是nil。这当然是一个非常不好的习惯。  


	(defun show-squares (start end)
	  (do ((i start (+ i 1)))
		  ((> i end) 'done)
		(format t "~A ~A~%" i (* i i))))


看到这里应该联想到let,这里也可以在初始化列表里定义多个变量。
如果后定义的变量引用了前定义的变量，会怎么样呢？
还记得let会怎么样么？
这里也是那样的。  


	CL-USER> 
	(let ((x 'a))
	  (do ((x 1 (+ x 1))
	       (y x x))
	      ((> x 5))
	    (format t "(~A ~A) " x y)))
	(1 A) (2 1) (3 2) (4 3) (5 4) NIL
	CL-USER> 


同样有一个do\*可以完成前面期望的结果。  


	CL-USER> 
	(do\* ((x 1 (+ x 1))
	       (y x x))
	      ((> x 5))
	    (format t "(~A ~A) " x y))
	(1 1) (2 2) (3 3) (4 4) (5 5) NIL
	CL-USER> 


还有一些专门的迭代操作符。如遍历列表的dolist     


	CL-USER> (dolist (x '(a b c d) 'done)
		   (format t "~A " x))
	A B C D DONE
	CL-USER> 


初始化列表的第三个参数可以省略，默认是nil。  


还有一个遍历次数的dotimes  


	CL-USER> (dotimes (x 5 x)
		   (format t "~A " x))
	0 1 2 3 4 5
	CL-USER> 	


注意这里的5是由初始化列表的第三个参数给出的。同样也可以省略，默认为nil。  


这里介绍一个比dolist还要有意思的迭代操作符mapc   


	CL-USER> (mapc #'(lambda (x y)
			   (format t "~A ~A | " x y))
		       '(hip flip slip)
		       '(hop flop slop))
	HIP HOP | FLIP FLOP | SLIP SLOP | (HIP FLIP SLIP)
	CL-USER> 
	

它可以同时遍历多个列表，和mapcar类似。不过不会对结果做任何操作。
总是返回第二个参数。  


##5.5 多个值  


在前面我们已经看到gethash可以返回多个值，同样我们自定义函数时也可以返回多个值。
这样就不用建立一个结构体来返回多个值。
get-decoded-time就会返回九个值来表示时间。  


返回多个值还可以用于查找，明确区分找到的是nil，还是没有找到。
因为我们完全可以把nil插入hash表里，这就是gethash为什么会返回两个值。  


values可以构造多个值  


	CL-USER> (values 'a nil (+ 1 2))
	A
	NIL
	3
	CL-USER> 


这样我们就可以把values表达式作为函数的最后一个表达式，就可以返回多个值了。  


	CL-USER> ((lambda () ((lambda () (values 1 2)))))
	1
	2
	CL-USER> 


如果我们使用values返回多个值，但是只有一个变量接收，那么只能接收第一个数。  


	CL-USER> 
	(let ((x (values 1 2)))
	  x)
	1
	CL-USER> 


values如果没有参数，那么就什么也没有。
如果用变量来接这样的表达式，那么就会得到nil。  


	CL-USER> (values)
	; No values
	CL-USER> 
	(let ((x (values)))
	  x)
	NIL
	CL-USER> 


如果想真正接收多个值，可以用multiple-value-bind  


	CL-USER> (multiple-value-bind (x y z) (values 1 2 3) (list x y z))
	(1 2 3)
	CL-USER> 


如果接的变量多，给的值数量少，那么多余的变量就是nil   


	CL-USER> (multiple-value-bind (x y z) (values 1 2) (list x y z))
	(1 2 NIL)
	CL-USER> 


同样，如果接的变量少，给的值数量多，那么多余的值就忽略了。  


	CL-USER> (multiple-value-bind (s m h) (get-decoded-time)
		   (format nil "~A:~A:~A" h m s))
	"23:20:45"
	CL-USER> 
	

还可以使用multiple-value-call，把多个值作为参数传递给某函数。  


	CL-USER> (multiple-value-call #'+ (values 1 2 3))
	6
	CL-USER> 


还可以使用multiple-value-list，使用多个值直接构造列表  


	CL-USER> (multiple-value-list (values 1 2 3))
	(1 2 3)
	CL-USER> 


这个操作符就好象下面这个操作一样  


	CL-USER> (multiple-value-call #'list (values 1 2 3))
	(1 2 3)
	CL-USER> 


##5.6 异常终止  


我们可以使用return或者return-from来修改控制流程，直接返回。
不过有时还需要抛出异常，来更明显的把控制流程转交给上层。
这就是catch和throw。
catch第一个参数是一个标签，之后是一系列表达式。
throw时指定标签，一层一层向上抛异常，直到找到那个标签。  


	(defun super ()
	  (catch 'abort
	    (sub)
	    (format t "We'll Never see this")))
	
	(defun sub ()
	  (throw 'abort 99))


	CL-USER> (super)
	99
	CL-USER> 	


如果throw的标签，没有对应的catch，那么就会引起一个错误   


还可以使用error来抛出异常，不过它不会一层一层的往上抛异常，而是直接把控制权交给lisp系统，也就是终止了你的程序。  


如果希望即使抛出异常，某些表达式还是会被执行，就像很多人在c/c++里利用析构函数一样，在lisp里，我们可以使用
unwind-protect。它会抛出异常，并且它还会把它的所有表达式都执行完毕。  


	CL-USER> (setf x 1)
	1
	CL-USER> (catch 'abort
		   (unwind-protect
		     (throw 'abort 99)
		     (setf x 2)))
	99
	CL-USER> x
	2
	CL-USER> 


同时它把它的第一个参数作为值返回。  


##5.7 日期算法  


在很多程序中，对日期进行加减乘除是很重要的一个功能。
在这里我们先把日期转换为从2000年1月1日到某日期的天数，之后再使用数字的加减乘除，之后再转换回日期。  


为了把日期转换为数字，要把年月日分别转换为天数，然后再统一加起来。  


首先我们需要一张表格，把每月对应的天数列出来，在非润年的情况下。我们先使用列表来表示  

	
	CL-USER> (setf mon '(31 28 31 30 31 30 31 31 30 31 30 31))
	(31 28 31 30 31 30 31 31 30 31 30 31)


我们测一下  


	CL-USER> (apply #'+ mon)
	365


如果我们把列表倒过来，再用maplist和+操作符，就可以得出一年开始到某月的天数。  


	CL-USER> (setf nom (reverse mon))
	(31 30 31 30 31 31 30 31 30 31 28 31)
	CL-USER> (setf sums (maplist #'(lambda (x)
					 (apply #'+ x))
				     nom))
	(365 334 304 273 243 212 181 151 120 90 59 31)
	CL-USER> (reverse sums)
	(31 59 90 120 151 181 212 243 273 304 334 365)
	CL-USER> 


为了效率，我们在5.1里使用vector来表示这最后的结果。  


![5.1](/assets/ol-acl-5.1-1.png)
![5.1](/assets/ol-acl-5.1-2.png)  


润年就是年数能被4整除但是不能被100整除，或者能被400整除。
这两种情况下就是润年。所以1904年是润年，而1900年不是，1600年就是。  


date->num把日期转换为数字，把年月日的天数分别计算出来，加在一起。
month-num查找到当年到那月初的天数，如果是2月之后，并且是润年，就再加1。
year-num计算从2000年1月1日到当年1月1日所经历的天数，注意，如果是2000年前，那么就是负数。  


![5.2](/assets/ol-acl-5.2-1.png)
![5.2](/assets/ol-acl-5.2-2.png)  


num->date把数字转换会日期。
num-year把日期算出来，还返回剩余天数。
num-month再从剩余天数中算出月份。  


num-year一年一年的向上或向下数，直到算出来的天数大于等于n。
如果向下数，可以直接把当前算的年份返回。
如果向上数，需要把前面一次迭代的年份返回，这就是prev变量的意义。  


num-month就是从month数组中找对应的月份。  


这样对于日期的计算就简单了很多。这里有一个date+方便我们测试  


	CL-USER> (multiple-value-list (date+ 17 12 1997 60))
	(15 2 1998)
	CL-USER> 