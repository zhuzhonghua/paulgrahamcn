---
layout: post
title : "第10章--宏(Macros)"
category : acl
tags : [acl]
---
{% include JB/setup %}


列表既是Lisp代码，也是Lisp的对象。这是如何跨越的呢？  


##10.1 执行(Eval)  


调用list可以生成表达式，如何让lisp来执行那些表达式呢？eval  

	
	CL-USER> (eval '(+ 1 2 3))
	6
	CL-USER> (eval '(format t "hello~%"))
	hello
	NIL


我们用eval来实现一个简单的toplevel  

	
	(defun our-toplevel ()
	  (do ()
	      (nil)
	    (format t "~%>")
	    (print (eval (read)))))


所以toplevel也被称为REPL(Read-Eval-Print-Loop).   


调用eval跨越了代码和数据的界限。
但是   


1. 效率不高，相对于执行编译好的代码。
2. 没有上下文，如果在let中调用eval，无法引用let中定义的变量。   


所以eval一般用于toplevel，其它情况用别的方法。  
但是eval提供了一个很好的概念模型，我们可以想象eval是一个很大的cond表达式  

	
	(defun eval (expr env)
	  (cond ...
		((eql (car expr) 'quote) (cadr expr))
		...
		(t
		 (apply (symbol-function (car expr))
			(mapcar #'(lambda (x)
				    (eval x env)))))))


quote为了保护后面的列表不被求值，而大多数其它情况都是要被求值的。


coerce和compile与eval类似。
强制把lambda表达式转换为函数  
	
	
	CL-USER> (coerce '(lambda (x) x) 'function)
	#<FUNCTION :LAMBDA (X) X>


如果compile的第一个参数为nil，那么第二个参数也会被编译为lambda表达式。  

	
	CL-USER> (compile nil '(lambda (x) (+ x 2)))
	#<COMPILED-FUNCTION NIL>
	NIL
	NIL	


虽然它们都跨越了代码和数据的界限，但是问题是一样的，相对于执行编译好的代码，效率不高。  


##10.2 宏(Macros)  


使用宏，可以让程序来写程序。这就是Lisp的宏与其它语言的宏不同的地方。
宏的内容体是转换。使用defmacro，我们来定义一个宏。  
	
	
	(defmacro nil! (x)
	  (list 'setf x nil))


在宏的内部，是构造一个转换过程。
调用(nil! x)时，就是调用了(setf x nil)。
并且这个转换过程是在编译期做的，所以不会对程序性能有影响。  

	
	CL-USER> (nil! x)
	NIL
	CL-USER> x
	NIL


通过调用函数来测试函数，而对于宏，要看它的展开形式。

	
	CL-USER> (macroexpand-1 '(nil! x))
	(SETF X NIL)
	T


展开后的宏有可能是另外一个宏，那么Lisp会继续展开。直到不再是宏为止。  


所谓知其然知其所以然，宏是如何实现的呢？在底层，它类似一个这样的函数  

	
	(lambda (expr)
	  (apply '(lambda (x) (list 'set x nil))
		 (cdr expr)))


当我们把(nil! x)传递给expr时，它就转换为那样一个表达式。  


##10.3 反引号(Backquote)  


在我的电脑键盘上，反引号是在Esc下面，与～一个键。
反引号与引号(quote)有同样的功能  

	
	CL-USER> `(a b c)
	(A B C)  


同样也会保护表达式，不被求值。
但是如果在backquote表达式内有,(comma)和,@(comma-at)，那么那么，或者，@后面的表达式就会被求值。  

	
	CL-USER> (setf a 1 b 2)
	2
	CL-USER> `(a is ,a and b is ,b)
	(A IS 1 AND B IS 2)


相对于使用list，这样生成的表达式更直接一些。如nil!  
	
	(defmacro nil! (x)
	  `(setf ,x nil))


comma-at与comma类似，但是会拆分参数，也就是说comma-at后面的参数必须是列表。  

	
	CL-USER> (setf lst '(a b c))
	(A B C)
	CL-USER> `(lst is ,lst)
	(LST IS (A B C))
	CL-USER> `(lst elements is ,@lst)
	(LST ELEMENTS IS A B C)


comma-at与comma在宏的定义中是非常有用的。如我们定义一个while循环  

	
	(defmacro while (test &rest body)
	  `(do ()
	       ((not ,test))
	     ,@body))


rest收集除条件判断外的所有参数，comma-at把参数拆分，拆分结果作为do的函数体  

	     
	CL-USER> 
	(let ((x 0))
	  (while (< x 10)
	    (princ x)
	    (incf x)))
	0123456789NIL
	CL-USER> 
	

##10.4 例子：快速排序(Quicksort)   


快速排序的基本思想就是  
1. 把要排序的数组分为两部分，以中间的元素作为参考  
2. 把左边大于中间元素的元素，与右面小于中间元素的元素进行交换  
3. 分而治之，分别对左右两部分数据再应用这个过程       


![10.1](/assets/ol-acl-10.1.png)  


使用宏的实现如10.1所示  


##10.5 宏设计  


写宏是一个别样的过程，在写的过程中，你就好象一位语言设计者。  


但是在写宏的过程中有一些比较容易犯的错误，这里提醒一下。 
1. 变量捕捉
2. 多次执行  


以一个宏ntimes为例    

	
	(ntimes 10
		(princ “.”))
	

ntimes可能如上使用，如我们如下定义ntimes  

	
	(defmacro ntimes (n &rest body)
	  `(do ((x 0 (+ x 1)))
	       ((not (> x )))
	     ,@body))


虽然这个定义能满足上面的需求，但是如果body的表达式内，使用了变量x，会发生什么事情呢？  

	
	(let ((x 10))
	  (ntimes 5
		  (setf x (+ x 1)))
	  x)


如上面的表达式，我们期望x应该是15，但是扩展后，会变成如下样子  

		  
	(let ((x 10))
	  (do ((x 0 (+ x 1)))
	      ((>= x 5))
	    (setf x (+ x 1)))
	  x)


这样最终的x值是多少呢？   


那么在宏定义内不能使用普通的变量声明。
那么如何来计数呢？使用前面的gensym。这样与环境变量是不会冲突的。  

	
	(defmacro ntimes (n &rest body)
	  (let ((g (gensym)))
	    `(do ((,g 0 (+ ,g 1)))
		 ((>= ,g n))
	       ,@body)))
	

这样虽然就不会变量冲突，但是还会出现多次执行的错误。
因为参数n有可能是表达式，如果直接放在宏定义内，会多次执行。  
如这样的调用   

	
	(let ((v 10))
	  (ntimes (setf v (- v 1))
		  (princ ".")))
	
	
扩展后如下   

	
	(let ((v 10))
	  (do ((#:g1 0 (+ #:g1 1)))
	      ((>= #:g1 (setf v (- v 1))))
	    (princ ".")))	  


所以应该通过gensym，来保存n的求值结果  

	
	(defmacro ntimes (n &rest body)
	  (let ((g (gensym))
		(h (gensym)))
	    `(let ((,h ,n))
	       (do ((,g 0 (+ ,g 1)))
		   ((>= ,g ,h))
		 ,@body))))


这样基本就正确了，虽然还会遇到一些别的问题。但是随着经验的增长，会越加的熟练。  


在测试宏时，可以使用pprint与macroexpand-1配合，查看我们设计的宏最终扩展的代码， 如


	CL-USER> 
	
	(pprint (macroexpand-1 '(cond (a b)
				      (c d e)
				      (t f))))
	
	(IF A B (IF C (PROGN D E) F)); No values
	CL-USER> 



##10.6 Generalized Reference  


如果把宏用于setf中，如下定义car的一个宏  


       (defmacro cah (lst) `(car ,lst))


这样是可以的，扩展后的(car lst)可以放在setf的第一个参数，所以(cah lst)也可以放在setf的第一个参数。  


       CL-USER> 
       (let ((x '(a b c)))
         (setf (cah x) 44)
         x)
       (44 B C)


但是很多情况下，并不正确，如假设incf定义如下：  

	
	(defmacro incf (x &optional (y 1))
	  `(setf ,x (+ ,x ,y)))       


对于这样的调用

	
	(incf (car (push 1 lst)))


就会扩展为  

		
	(setf (car (push 1 lst)) (1+ (car (push 1 lst))))	  


这是错误的。很明显看到(car (push 1 lst))被执行了两次。
即使使用前面的(gensym)也无法避免两次调用。    


这时候就要使用define-modify-macro，专门对setf之类的宏管用。
这时候定义前面的incf可以如下定义  

	
	(define-modify-macro our-incf (&optional (y 1)) +)


注意这里，前面的incf定义有参数x和可选参数y，而使用define-modify-macro没有要设置的x。
也就是对于our-incf的调用默认添加了setf x 之类的操作。  


再定义一个宏，也许会对define-modify-macro能够认识更清楚一些  


	(define-modify-macro append1f (val)
	  (lambda (lst val) (append lst (list val))))
	

	CL-USER> 
	(let ((lst '(a b c d)))
	  (append1f lst 'e)
	  lst)
	(A B C D E)


##10.7 例子：宏工具集  


我们来定义一些宏的工具来帮助我们后面写代码。如图10.2  


![10.2](/assets/ol-acl-10.2-1.png)
![10.2](/assets/ol-acl-10.2-2.png)  


for循环, 一个变量， 从起始值开始遍历到终止值， 每次执行后面的内容    

	
	CL-USER> 
	(for x 1 8
	     (princ x))
	12345678NIL


必须有一个变量来hold住stop值，因为每次都要拿变量与终止值进行比较。
而起始值只会在变量初始化时执行一次。  


in操作符，判断第一个参数是否在剩余参数中  

	
	(in (car expr) '+ '- '*)  


这样显然要比这样写简便许多  

	
	(let ((op (car expr)))
	  (or (eql op '+)
	      (eql op '-)
	      (eql op '*)))


random-choices 随机从后面的表达式中抽取一个，执行  


这样的表达式  

	
	(random-choices (turn-left) (turn-right))


扩展后，就是下面的代码 

	
	(case (random 2)
	  (0 (turn-left))
	  (1 (turn-right)))


所以定义中,@(let xxx)就是为了生成不同的case判断。  


with-gensyms主要用于宏定义，而不是用户逻辑中。
当我们需要定义好几个gensym变量时  
	
	(let ((x (gensym)) (y (gensym)) (z (gensym)))
	     ...)


使用with-gensyms，我们只需要  

	
	(with-gensyms (x y z)
	    ...)


当有些代码，没办法写成函数时，就可以考虑写成宏。
而avg既可以写成函数，也可以写成宏。   

	
	CL-USER> (avg 2 4 8)
	14/3


如果写成函数，可能是这样子  

	
	(defun avg (&rest args)
	  (/ (apply #'+ args) (length args)))


差别在于一个在编译期执行，一个在运行期执行，当然我们期望前者。  
	
	
	(defmacro aif (test then &optional else)
	  `(let ((it ,test))
	     (if it ,then ,else)))
	

最后一个宏aif故意犯了前面说的宏定义的一个毛病，变量捕捉(variable capture)。意图如下：  


假如我们写了这样一段代码  

	
	(let ((val (calc-sth)))
	  (if val
	      (1+ val)
	      0))
	

我们就可以换成这样写  

	
	(aif (calc-sth)
	     (1+ it)
	     0)


这里就使用了在aif宏内定义的变量it，不过这样的变量定义最好在项目中能有一个命名规范，防止命名冲突，或者误解。  


通过上面的例子，我们大概了解了宏的一个很重要的用途，那就是减少人工代码量。
你也可以简单粗暴有效的认为宏就是这样一个目的。
当我们可以写更少的代码完成相同的工作量时，那我们就是高效的。   


而对于某些人对于效率的质疑，只能用这句话回应：过早优化是万恶之源。
换一个方式理解这句话就是快速完成工作内容，之后根据具体情况，优化代码。   


##10.8 On Lisp  


站在巨人的肩膀上，可以看得更远。
使用Lisp可以让你站在Lisp的肩膀上，指数级的向上走。
宏就是这样一个秘密武器。
这就是On Lisp的内涵。   



