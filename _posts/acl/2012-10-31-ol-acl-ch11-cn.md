---
layout: post
title : "第11章--Common Lisp 对象系统(CLOS)"
category : acl
tags : [acl]
---
{% include JB/setup %}

ClOS中主要使用defclass和defmethod来定义行为。  


##11.1 面向对象编程  


面向对象编程可以很好的将方法与对象结合起来，并且把数据很好地封装到对象内部。


![11.1](/assets/ol-acl-11.1.png)
![11.2](/assets/ol-acl-11.2.png)


看11.1的代码，我们要定义一个方法area，根据不同的参数类型，使用不同的计算方法。
在area内部使用cond来区分参数。  


使用CLOS，我们也可以使用11.2的方法来定义代码。
我们定义不同的area方法，cl会根据参数查找相应的方法。
这可以看成是多态。  


还有继承，包括域和方法。
在11.2中defclass的第二个参数是空列表。
这里也可以填写父类。  

	
	(defclass colored ()
	  (color))
	
	
	(defclass colored-circle (circle colored)
	  ())
	

这样colored-circle就继承了circle和colored各自的成员。
并且它也继承了circle的area方法。
具体是指，当使用colored-circle的实例调用area时，就会调用前面的为circle定义的area方法。  


##11.2 类与实例  


前面看到如何定义class，定义class的实例时，使用make-instance  

	
	CL-USER> (setf c (make-instance 'circle))
	#S(CIRCLE :RADIUS NIL)


设置实例的成员值时使用setf和slot-value   

	
	CL-USER> (setf (slot-value c 'radius) 1)
	1


##11.3 成员属性  


defclass的第三个参数是成员列表。
可以只写出成员名，也可以指定相关的属性。  


\:accessor代替默认的slot-value，定义相应的方法名。  

	
	(defclass circle ()
	  ((radius :accessor circle-radius)
	   (center :accessor circle-center)))
	
	
	CL-USER> (setf c (make-instance 'circle))
	#<CIRCLE #x2106418E>
	CL-USER> (setf (circle-radius c) 1)
	1
	CL-USER> (circle-radius c)
	1


还可以单独使用:writer或者:reader来分别定义读写部分。  


指定成员的默认值使用:initform，为了能在make-instance时指定成员值，使用:initarg   

	
	(defclass circle ()
	  ((radius :accessor circle-radius
		   :initarg  :radius
		   :initform 1)
	   (center :accessor circle-center
		   :initarg  :center
		   :initform (cons 0 0))))

	
	CL-USER> (setf c (make-instance 'circle :radius 3))
	#<CIRCLE #x214F4996>
	CL-USER> (circle-radius c)
	3
	CL-USER> (circle-center c)
	(0 . 0)
	CL-USER>


\:initarg必须放在:initform的前面，如果有的话。


类的静态成员通过属性参数:allocation :class来声明。  

	
	CL-USER> 
	
	(setf daily-blab        (make-instance 'tabloid)
	      unsolicited-mail  (make-instance 'tabloid))
	
	#<TABLOID #x215562AE>
	CL-USER> (setf (tabloid-story daily-blab) 'adultery-of-senator)
	ADULTERY-OF-SENATOR
	CL-USER> (tabloid-story unsolicited-mail)
	ADULTERY-OF-SENATOR
	CL-USER> 


##11.4 父类  
	

defclass 的第二个参数就是父类的列表，如  


	(defclass graphic ()
	  ((color :accessor graphic-color :initarg :color)
	   (visible :accessor graphic-visible :initarg :visible
		    :initform t)))
	
	(defclass screen-circle (circle graphic)
	  ())


screen-circle默认拥有circle和graphic所有的成员，还有相关的方法。  
	
	
	CL-USER> 
	(graphic-color (make-instance 'screen-circle
				      :color 'red :radius 3))
	RED


还可以在子类修改成员的属性。  


	
	(defclass screen-circle (circle graphic)
	  ((color :initform 'purple)))

	  
	CL-USER> (graphic-color (make-instance 'screen-circle))
	PURPLE


##11.5 优先级(Precedence)


虽然c++有多重继承的功能，但是因为很多原因，在工作中并不建议使用多重继承。
在java里只能继承一个类，其它的都是接口。  


在Lisp中也可以使用多重继承，当需要把类的实例向上抽象为某个父类时(如查找相应的方法)，会用到这节讲的方法。

如下面这些类定义  


	(defclass sculpture () (height width depth))
	
	(defclass statue (sculpture) (subject))
	
	(defclass metalwork () (metal-type))
	
	(defclass casting (metalwork) ())
	
	(defclass cast-statue (statue casting) ())


![11.3](/assets/ol-acl-11.3.png)  

	
对于这样复杂的继承关系，向上抽象时顺序如11.3所示。
关键一点就是，如果有两个父类可以抽象为同一个父类，那么使用最右面的子类向上抽象，而不是使用最左边的子类向上抽象。
虽然很绕口，但是基本原则就是，在向上抽象是不能出现矛盾，子类永远不能出现在父类的上面。  


##11.6 通用函数(Generic Functions)  


在前面我们看到area方法，虽然有多个area方法，但是参数的类型不同，调用时也会找到相应的方法。
参数还可以有类型，无类型，有父类对应的方法子类也可以使用，甚至是基本类型，如字符串，数字等。  


以下面的方法为例  


	(defmethod combine (x y)
	  (list x y))
	
	CL-USER> (combine 'a 'b)
	(A B)


再定义一些继承类  

	
	(defclass stuff () ((name :accessor name :initarg :name)))
	(defclass ice-cream (stuff) ())
	(defclass topping (stuff) ())


我们再定义一个combine方法  

	
	(defmethod combine ((ic ice-cream) (top topping))
	  (format nil "~A ice-cream with ~A topping"
		  (name ic)
		  (name top)))


当使用特定的变量调用combine方法时，系统就会根据参数的类型找到相应的方法  

		  
	CL-USER> 
	(combine (make-instance 'ice-cream :name 'fig)
		 (make-instance 'topping :name 'treacle))
	"FIG ice-cream with TREACLE topping"

	
	CL-USER> (combine 23 'kiddoo)
	(23 KIDDOO)


定义方法时，还可以单独为一部分参数指定类型，在调用时lisp也能找到具体调用哪个方法。  


当指定第一个参数为ice-cream，第二个参数为字符串时，调用如下  


	(combine (make-instance 'ice-cream :name 'clam)
		 'abc)
	"CLAM ice-cream with ABC "


当指定第一个参数为ice-cream，第二个参数为topping时，调用如下  


	CL-USER> 
	(combine (make-instance 'ice-cream :name 'grape)
		 (make-instance 'topping :name 'marshmallow))
	"GRAPE ice-cream with MARSHMALLOW topping"
	CL-USER> 


除了自定义类型和无类型，还可以指定基本类型如数字  

	
	(defmethod combine ((x number) (y number))
	  (+ x y))


甚至还可以特定为某个具体数值  

	  
	(defmethod combine ((x (eql 'powder)) (y (eql 'spark)))
	  'boom)


但是所有定义的这些通用方法之间必须和谐，也就是参数数量啊，可选参数啊，关键字参数等等之间要和谐。
当定义不和谐时，系统会报错，所以最好就是一边写代码，一边测试。  


##11.7 辅助方法(Auxiliary Method)  


方法本身还可以有辅助方法，真是太神奇了。
辅助方法可以用来测试，控制流程等特殊目的。  


有两个辅助方法before和after，也就是在方法调用之前调用和在方法调用之后调用。如  

	
	(defclass speaker () ())
	
	(defmethod speak ((s speaker) string)
	  (format t "~A string" string))
		
	
	CL-USER> (speak (make-instance 'speaker) "I'm Humgry")
	I'm Humgry stringNIL


我们要对speak定义before和after方法  


	(defclass intellectual (speaker) ())
	
	(defmethod speak :before ((i intellectual) string)
	  (princ "perhaps "))
	
	(defmethod speak :after ((i intellectual) string)
	  (princ " in some sense"))
	
	
	CL-USER> (speak (make-instance 'intellectual) "I'm hungry")
	perhaps I'm hungry string in some senseNIL


但是最为函数调用的副作用，返回值还是speak的返回值。  


如果为speaker再定义一个speak的before方法呢？
	
	
	(defmethod speak :before ((s speaker) string)
	  (princ " I think "))
	
	CL-USER> (speak (make-instance 'intellectual) "I'm hungry")
	perhaps  I think I'm hungry string in some senseNIL	


结论是，根据继承关系，依次调用了。  


还有一个辅助方法是around方法。
当我某给speak定义around方法时，around就代替了原函数。
如果还需要原来的speak方法，那就只能在around方法内调用call-next-method来调用原函数了。  

	
	(defclass courtier (speaker) ())
	
	(defmethod speak :around ((c courtier) string)
	  (format t "does the king believe ~A?" string)
	  (if (eql (read) 'yes)
	      (if (next-method-p) (call-next-method))
	      (format t "indeed it'a a bad idea ~%"))
	  'bow)

		  
	CL-USER> (speak (make-instance 'courtier) "king will last")
	does the king believe king will last?yes
	 I think king will last stringBOW
	CL-USER> (speak (make-instance 'courtier) " the world is round")
	does the king believe  the world is round?no
	indeed it'a a bad idea 
	BOW


这样around就把原方法保护了起来。
并且返回值也是around辅助方法了。  


##11.8 方法组合(Method Combination)


在调用通用方法时，调用的永远是最适合参数或最特定参数的一个方法。
如果我们希望把所有这些可适用的方法的结果值都收集起来呢？
如对所有这些方法采用+操作符。
当然我们可以自己写一个，依次把参数转换为特定类型，然后调用方法。
然后再+起来。  

	
	(defun price (&rest args)
	  (+ (apply (xxxxx) args)
	     (apply (xxxxx) args)
	     .....))


除了这种方法之外，我们还可以使用(:method-combination )配合defgeneric来实现  


	(defgeneric price (x)
	  (:method-combination +))
		
	(defclass jacket () ())
	(defclass trousers () ())
	(defclass suit (jacket trousers) ())
	(defmethod price + ((jk jacket)) 350)
	(defmethod price + ((tr trousers)) 250)
	
	
	CL-USER> (price (make-instance 'suit))
	600


当然还有很多可以作为method-combination，and, append, list, max, min等等。  


##11.9 封装(Encapsulation)  


面向对象编程就是很好的把外部行为，对外接口功能，与内部实现，数据分割开。
你可以随便修改内部实现，而不影响外部接口。  


例如下面我们定义对外的两个接口increment和clear。  

	
	(defpackage "CTR"
	  (:use "COMMON-LISP")
	  (:export "COUNTER" "INCREMENT" "CLEAR"))
	
	
	(in-package ctr)
	
	(defclass counter () ((state :initform 0)))
	
	(defmethod increment ((c counter))
	  (incf (slot-value c 'state)))
	
	(defmethod clear ((c counter))
	  (setf (slot-value c 'state) 0))


虽然外部不许要关心counter里的state，但是外部还可以可以获取到这个name的。
可以使用(unintern 'state)，来加上这个限制。  


##11.10 两种方式(Two Models)  


面向对象的编程既可以通过消息传递，也可以通过通用函数来实现。
假设采用消息传递的方式，方法是属于对象的。

	
	tell obj area


不管obj内部使用什么方法，我们只是使用tell给obj发送了一个指令，消息。或者再加几个参数  

	
	tell obj move 10


或者
	
	(move obj 10)


但是这样总是有一些限制，没办法对参数进行具化，更没办法对部分参数进行具化，或者向上抽象。  


