---
layout: post
title : "第12章--底层结构"
category : acl
tags : [acl]
---
{% include JB/setup %}


结构体可以包含任何数据，甚至是结构体本身。
但是这并不一定是好事。
好与不好要看，这样做是无意的，还是故意的。  


##12.1 共享结构(Shared Structure)  


看下面这段代码，part是whole的一部分  

	
	CL-USER> (setf part (list 'b 'c))
	(B C)
	CL-USER> (setf whole (cons 'a part))
	(A B C)


![12.1](/assets/ol-acl-12.1.png)  


底层结构如12.1图所示。  


这样就是两个列表共享结构了。tailp可以用来检测，某列表是否是另一列表的一部分。   

	
	CL-USER> (tailp part whole)
	T


tailp的实现大概如下  

	
	(defun our-tailp (x y)
	  (or (eql x y)
	      (and (consp y)
		   (our-tailp x (cdr y)))))
	


![12.2](/assets/ol-acl-12.2.png)  


12.2中，whole1和whole2也共享结构，虽然whole1和whole2并不是对方的一部分。创建代码如下  

	
	CL-USER> (setf part '(b c)
		       whole1 (cons 1 part)
		       whole2 (cons 2 part))


但是下面这段代码创建的底层结构不是共享结构，而是共享的子树  

	
	CL-USER> (setf element (list 'a 'b)
		       hold1 (list 1 element 2)
		       hold2 (list element 3))


创建的底层结构如12.4所示。  

		       
![12.3](/assets/ol-acl-12.3.png)
![12.4](/assets/ol-acl-12.4.png)    


这里说明一下共享结构与共享子树的区别。   


当前面写代码copy-list与copy-tree时，最大的区别就是是否对car的元素进行递归调用。
这里也是，hold1和hold2共享的是某元素的car。这就是共享结构与共享子树的区别。
可与12.3进行对比。  


Java里经常说深拷贝与浅拷贝，也经常会出问题。
而在C++中，我们有指针和非指针变量，并在结构体里实现拷贝构造和=。
在Lisp中，为了避免对原数据修改，也许要拷贝一份。  


拷贝时就要区别对待，拷贝的是列表，还是子树，区别前面也讲了。  


	(defun our-copy-list (lst)
	  (if (null lst)
	      nil
	      (cons (car lst) (our-copy-list (cdr lst)))))
	
	(defun our-copy-tree (lst)
	  (if (atom lst)
	      lst
	      (cons (our-copy-tree (car lst))
		    (our-copy-tree (cdr lst)))))
	

![12.5](/assets/ol-acl-12.5.png)  


如图12.5所示


##12.2 修改(Modification)  


前面说结构包含结构，是好是坏，要看是故意这样做，还是无意的。
因为当我某修改了某结构，其它包含该结构的变量或列表也随之被修改了。
也就是我们在C++中使用指针或者引用达到的效果。  
	
	
	CL-USER> (setf whole (list 'a 'b 'c)
		       tail (cdr whole))
	(B C)
	CL-USER> (setf (car tail) 1)
	1
	CL-USER> whole
	(A 1 C)


##12.3 举例：队列   


![12.6](/assets/ol-acl-12.6.png)  
![12.7](/assets/ol-acl-12.7.png)  


使用pop和push很容易实现栈，而队列是FIFO，先进先出。实现代码如12.7所示。  

	
	CL-USER> (setf q1 (make-queue))
	(NIL)
	CL-USER> (progn (enqueue 'a q1)
			(enqueue 'b q1)
			(enqueue 'c q1))
	(A B C)
	CL-USER> q1
	((A B C) C)
	CL-USER> (dequeue q1)
	A
	CL-USER> (dequeue q1)
	B
	CL-USER> (enqueue 'd q1)
	(C D)
	CL-USER> 


##12.4 破坏性函数(Destructive Functions)  


当考虑效率问题时，应该使用修改函数，而不是每修改添加一个元素，就拷贝一份所有数据。  


delete是remove的修改性版本。  

	
	CL-USER> (setf lst '(a r a b i a))
	(A R A B I A)
	CL-USER> (delete 'a lst)
	(R B I)
	CL-USER> lst
	(A R B I)


delete破坏了lst，但是并没有把第一个a删除，所以如果真正想达到效果，应该使用  
	

	(setf lst (delete 'a lst))


nconc是append的修改性版本。内部实现可能如下所示  
	
	
	(defun nconc2 (x y)
	  (if (consp x)
	      (progn
		(setf (cdr (last x)) y)
		x)
	      y))
	

mapcan类似mapcar，但是它会把结果拼接起来，类似下面的实现代码    

	
	(defun our-mapcan (fn &rest lsts)
	  (apply #'nconc (apply #'mapcar fn lsts)))


所以使用mapcan时应该注意。  

	
	CL-USER> (setf a '(a b c))
	(A B C)
	CL-USER> (setf b '(1 2 3 4))
	(1 2 3 4)
	CL-USER> (mapcan #'list
			 a
			 b)
	(A 1 B 2 C 3)
	CL-USER> 


虽然这里演示的并没有修改a或者b，但是还是应该注意mapcan的底层实现。
mapcan可以用于返回一层节点。假设children返回某元素的子节点，我们实现grandchildren如下   

	
	(defun grandchildren (x)
	  (mapcan #'(lambda (c)
		      (copy-list (children c)))
		  (children x)))
  

我们也可以用append来实现mapcan的非修改版本   

	
	(defun mappend (fn &rest lsts)
	  (apply #'append (apply #'mapcar fn lsts)))


使用mappend来实现grandchildren如下  


	(defun grandchildren (x)
	  (mappend #'children (children x)))


##12.5 二叉搜索树  


![12.8](/assets/ol-acl-12.8.png)
![12.8](/assets/ol-acl-12.8-2.png)  


构造二叉树时，我们应该直接修改传入的参数，而不是像前面那样拷贝一份所有的数据。
这样更直接，效率也更高。
12.8的代码就是这样一个版本。  

	
	CL-USER> (setf *bst* nil)
	NIL
	CL-USER> (dolist (x '(7 2 9 8 4 1 5 12))
		   (setf *bst* (bst-insert! x *bst* #'<)))
	NIL
	CL-USER> 
	

![12.9](/assets/ol-acl-12.9-1.png)
![12.9](/assets/ol-acl-12.9-2.png)  


12.9显示的是从二叉树中删除元素的代码，当然也是直接修改传入的参数。  

	
	CL-USER> (setf *bst* (bst-delete 2 *bst* #'<))
	
	#S(NODE
	   \:L
	   #S(NODE :L #S(NODE :L NIL :ELT 1 :R NIL) :ELT 4
	      \:R #S(NODE :L NIL :ELT 4 :R #S(NODE :L NIL :ELT 5 :R NIL)))
	   \:ELT 7
	   \:R
	   #S(NODE :L #S(NODE :L NIL :ELT 8 :R NIL) :ELT 9
	      \:R #S(NODE :L NIL :ELT 12 :R NIL)))
	CL-USER> (bst-find 2 *bst* #'<)
	NIL
	CL-USER> 


##12.6 双向链表  


![12.10](/assets/ol-acl-12.10-1.png)
![12.10](/assets/ol-acl-12.10-2.png)  


我们可以把lisp最基本的数据结构，理解为单向列表。只能从前向后顺序遍历。
而双向链表是要也可以从后向前遍历。  


图12.10就是双向链表的代码实现。
数据结构中要有一个字段保留当前节点的值，还有一个prev和next节点，指向当前节点的前后两个节点。
通过这种方式，就可以实现两个方向遍历了。  


dl->list把一个双向链表用cons表示出来。
在删除节点和添加节点时，要注意设置各自前后节点的顺序。
dl-list把参数表示为双向链表，然后返回该双向链表。


	CL-USER> (dl-list 'a 'b 'c)
	#<DL (A B C)>


dl-list使用了reduce，:from-end ，就相当与下面的代码  
	

	(dl-insert 'a (dl-insert 'b (dl-insert 'c nil)))


可以这样使用这些代码  

	
	CL-USER> (setf dl (dl-list 'a 'b))
	#<DL (A B)>
	CL-USER> (setf dl (dl-insert 'c dl))
	#<DL (C A B)>
	CL-USER> (dl-insert 'r (dl-next dl))
	#<DL (R A B)>
	CL-USER> dl
	#<DL (C R A B)>
	CL-USER> 


##12.7 环形结构  


![12.12](/assets/ol-acl-12.12.png)  


列表的car和cdr可以指向别的值，那么当然也可以指向自己。
如图12.12，我们看到这种结构的底层。  

	
	CL-USER> (setf x (list 'a))
	(A)
	CL-USER> (progn (setf (cdr x) x) nil)
	NIL
	CL-USER> 


这里让x的cdr指向自己。
我们这样做时，使用了progn，并且返回为nil。
防止系统在输出表示x时出现死循环。  


如果一定要输出表示x，那么既得把*print-circle*设置为t。  

	
	CL-USER> (setf *print-circle* t)
	T
	CL-USER> x
	#1=(A . #1#)
	CL-USER> 


还可以让car指向自己。  

	
	CL-USER> (let ((y (list nil)))
		   (setf (car y) y)
		   y)
	#1=(#1#)
	CL-USER> 


甚至可以让car和cdr都指向自己  

	
	CL-USER> (let ((c (cons nil nil)))
		   (setf (car c) c
			 (cdr c) c)
		   c)
	#1=(#1# . #1#)
	CL-USER> 


循环结构好像没什么实际用途，只不过提醒我们在实际写代码时，别无意的写成这种形式。  


数组也可以让元素指向自己。  

	
	CL-USER> (setf *print-array* t)
	T
	CL-USER> (let ((a (make-array 1)))
		   (setf (aref a 0) a)
		   a)
	#1=#(#1#)
	CL-USER> 


这种情况也经常出现在结构体中，比如父节点和子节点的关系。  

	
	CL-USER> (progn (defstruct nod
			  (parent nil) (child nil))
			(let ((c (make-nod))
			      (p (make-nod)))
			  (setf (nod-parent c) p
				(nod-child  p) c)
			  c))
	#1=#S(NOD :PARENT #S(NOD :PARENT NIL :CHILD #1#) :CHILD NIL)


##12.8 常量结构  


常量在程序中很有用，一方面可以提高程序效率，更可以告诉别的代码书写者，不应该修改这个东东。


我们看下面这个函数  

	
	(defun arith-op (x)
	  (member x '(+ - * /)))
	

'(+ - * /)就是一个常量。  

	  
这样当我们使用该函数时  

	
	CL-USER> (arith-op '+)
	(+ - * /)
	CL-USER> (arith-op '-)
	(- * /)
	CL-USER> (arith-op 'a)
	NIL


于是当我们使用nconc与arith-op结合时  

	
	CL-USER> (nconc (arith-op '*) '(as it were))
	(* / AS IT WERE)


因为nconc是会修改参数的，所以会产生下面的结果  


	CL-USER> (arith-op 'as)
	(AS IT WERE)
	CL-USER> 


出现这样的错误，有时候很不容易查。
避免方法当然有很多，如  

	
	(defun arith-op (x)
	  (member x (list '+ '- '* '/)))


这样不太好，每次都构造一个新的列表出来。
更好的方法是使用find代替member  
	
	
	(defun arith-op (x)
	  (find x '(+ - * /)))


这是在处理list时出现的错误，在其它情况下也会出现这样的错误，数组，字符串，结构体，类实例等。
避免出现这类错误的好方法是写代码时明确使用的是会修改参数的函数，还是不会修改参数的函数。
当然还有别的方法，如闭包。  


